* DEMO.BP CRUD.RTNE
* WRAPPER FOR DEMO ROUTINE TO WORK WITH WDB.RESOURCE
******
*
WERR = "" ; BODY = "" ; RERR = ""
OPEN "WDB.RESOURCE" TO F.WORK ELSE WERR = "CANNOT OPEN WDB.RESOURCE"
OPEN "bin"          TO F.BIN  ELSE WERR = "CANNOT OPEN bin"
*
CALL WGETHEADER(REQUESTMETHOD,"REQUEST_METHOD")
CALL WGETPARAM(WID,"1")
IF REQUESTMETHOD = "" THEN REQUESTMETHOD = "VIEW"
*
BEGIN CASE
   CASE REQUESTMETHOD EQ "GET"
      IF WID NE "" THEN
         READ WREC FROM F.WORK,"API*":WID THEN
            WIDS = \{\
            WIDS = WIDS: \ "id" : "\:WID:\",\
            WIDS = WIDS: \ "A1" : "\:WREC<1>:\",\
            WIDS = WIDS: \ "desc" : "\:WREC<2>:\",\
            WIDS = WIDS: \ "program" : "\:WREC<3>:\",\
            WIDS = WIDS: \ "A4" : "\:WREC<4>:\",\
            WIDS = WIDS: \ "A5" : "\:WREC<5>:\",\
            WIDS = WIDS: \ "A6" : "\:WREC<6>:\",\
            WIDS = WIDS: \ "debug" : "\:WREC<7>:\"}\
         END
      END ELSE
         WIDS = ""
         SSELECT F.WORK
         JSONOBJ = ""; OBJ = ""
         EOF = 0
         LOOP         
            READNEXT WID ELSE EOF = 1
         UNTIL EOF DO            
            IF WID[1,3] = "API" THEN             
               WIDS<-1> = \{"id":"\:FIELD(WID,"*",2):\"}\
            END
         REPEAT
         WIDS = CHANGE(WIDS,@AM,\,\)
      END
      BODY = \ [\: WIDS :\] \

   CASE REQUESTMETHOD EQ "POST"
      *WERR = "POST or Create Method"
      CALL WGETBODY(BODY)
      CALL WOBJ(OBJ,"FROMSTRING","",BODY,"",RERR)
      CALL WOBJ(OBJ,"GET","id",WID,"",RERR)
      CALL WOBJ(OBJ,"GET","desc",DESC,"",RERR)
      CALL WOBJ(OBJ,"GET","program",PGM,"",RERR)
      CALL WOBJ(OBJ,"GET","debug",DEBUG,"",RERR)
      IF DEBUG = "true" THEN DEBUG = 1 ELSE DEBUG = ""
      WID = CHANGE(WID," ","_")
      READ WREC FROM F.WORK,"API*":WID THEN
         WERR = \{ "msg": "Cannot create \:WID:\, it already exists" }\
      END ELSE
         READ PCHECK FROM F.BIN,PGM THEN
            WREC = ""
            WREC<1> = "P"
            WREC<2> = DESC
            WREC<3> = PGM
            WREC<4> = ""
            WREC<5> = 1
            WREC<6> = 1
            WREC<7> = DEBUG
            WRITE WREC ON F.WORK,"API*":WID
            MESSAGE = WID:\ has been added to WDB.RESOURCE\
         END ELSE
            MESSAGE = PGM:" is not a valid program, please correct"
         END
         BODY = \{ "msg": "\:MESSAGE:\"}\
      END

   CASE REQUESTMETHOD EQ "PUT"
      *WERR = "PUT or Update Method"
      CALL WGETBODY(BODY)
      CALL WOBJ(OBJ,"FROMSTRING","",BODY,"",RERR)
      CALL WOBJ(OBJ,"GET","id",WID,"",RERR)
      CALL WOBJ(OBJ,"GET","desc",DESC,"",RERR)
      CALL WOBJ(OBJ,"GET","program",PGM,"",RERR)
      CALL WOBJ(OBJ,"GET","debug",DEBUG,"",RERR)
      IF DEBUG = "true" THEN DEBUG = 1 ELSE DEBUG = ""
      READ WREC FROM F.WORK,"API*":WID THEN
         READ PCHECK FROM F.BIN,PGM THEN
            WREC<2> = DESC
            WREC<3> = PGM
            WREC<7> = DEBUG
            WRITE WREC ON F.WORK,"API*":WID
            MESSAGE = WID:\ has been updated!\
         END ELSE
            MESSAGE = PGM:" is not a valid program, please correct"
         END
      END ELSE
         MESSAGE = WID:\ does not exist\
      END
      BODY = \{ "msg": "\:MESSAGE:\"}\

   CASE REQUESTMETHOD EQ "DELETE"
      *WERR = "DELETE Method"
      IF WID # "" THEN
         DELETE F.WORK,"API*":WID
         MESSAGE = WID:\ has been DELETED\
      END ELSE
         MESSAGE = WID:\ cannot be deleted at this time\
      END
      BODY = \{ "msg": "\:MESSAGE:\"}\

   CASE REQUESTMETHOD EQ "OPTIONS"
      CALL WSETHEADER ("Access-Control-Allow-Headers","X-Requested-With, accept, content-type")

   CASE 1
      WERR = \{ "msg": "Error in your call" }\

END CASE
*
IF WERR NE "" OR RERR NE "" THEN
   IF RERR NE "" THEN WERR = RERR
   OUTPUT = WERR
END ELSE
   OUTPUT = BODY
END

PRINT OUTPUT
CALL WSETHEADER ("Access-Control-Allow-Origin","*")
CALL WSETHEADER ("Access-Control-Allow-Methods","PUT, DELETE, POST, GET")
CALL WSETCONTENTTYPE("application/json") 
CALL WSEND (OUTPUT) 
*
END
